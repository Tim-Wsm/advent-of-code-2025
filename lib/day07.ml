(* --- Day 7: Laboratories ---

You thank the cephalopods for the help and exit the trash compactor, finding
yourself in the familiar halls of a North Pole research wing.

Based on the large sign that says "teleporter hub", they seem to be researching
teleportation; you can't help but try it for yourself and step onto the large
yellow teleporter pad.

Suddenly, you find yourself in an unfamiliar room! The room has no doors; the
only way out is the teleporter. Unfortunately, the teleporter seems to be
leaking magic smoke.

Since this is a teleporter lab, there are lots of spare parts, manuals, and
diagnostic equipment lying around. After connecting one of the diagnostic
tools, it helpfully displays error code 0H-N0, which apparently means that
there's an issue with one of the tachyon manifolds.

You quickly locate a diagram of the tachyon manifold (your puzzle input). A
tachyon beam enters the manifold at the location marked S; tachyon beams always
move downward. Tachyon beams pass freely through empty space (.). However, if a
tachyon beam encounters a splitter (^), the beam is stopped; instead, a new
tachyon beam continues from the immediate left and from the immediate right of
the splitter.

For example:

.......S.......
...............
.......^.......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

In this example, the incoming tachyon beam (|) extends downward from S until it
reaches the first splitter:

.......S.......
.......|.......
.......^.......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

At that point, the original beam stops, and two new beams are emitted from the
splitter:

.......S.......
.......|.......
......|^|......
...............
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

Those beams continue downward until they reach more splitters:

.......S.......
.......|.......
......|^|......
......|.|......
......^.^......
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

At this point, the two splitters create a total of only three tachyon beams,
since they are both dumping tachyons into the same place between them:

.......S.......
.......|.......
......|^|......
......|.|......
.....|^|^|.....
...............
.....^.^.^.....
...............
....^.^...^....
...............
...^.^...^.^...
...............
..^...^.....^..
...............
.^.^.^.^.^...^.
...............

This process continues until all of the tachyon beams reach a splitter or exit
the manifold:

.......S.......
.......|.......
......|^|......
......|.|......
.....|^|^|.....
.....|.|.|.....
....|^|^|^|....
....|.|.|.|....
...|^|^|||^|...
...|.|.|||.|...
..|^|^|||^|^|..
..|.|.|||.|.|..
.|^|||^||.||^|.
.|.|||.||.||.|.
|^|^|^|^|^|||^|
|.|.|.|.|.|||.|

To repair the teleporter, you first need to understand the beam-splitting
properties of the tachyon manifold. In this example, a tachyon beam is split a
total of 21 times.

Analyze your manifold diagram. How many times will the beam be split?


--- Part Two ---

With your analysis of the manifold complete, you begin fixing the teleporter.
However, as you open the side of the teleporter to replace the broken manifold,
you are surprised to discover that it isn't a classical tachyon manifold - it's
a quantum tachyon manifold.

With a quantum tachyon manifold, only a single tachyon particle is sent through
the manifold. A tachyon particle takes both the left and right path of each
splitter encountered.

Since this is impossible, the manual recommends the many-worlds interpretation
of quantum tachyon splitting: each time a particle reaches a splitter, it's
actually time itself which splits. In one timeline, the particle went left, and
in the other timeline, the particle went right.

To fix the manifold, what you really need to know is the number of timelines
active after a single particle completes all of its possible journeys through
the manifold.

In the above example, there are many timelines. For instance, there's the
timeline where the particle always went left:

.......S.......
.......|.......
......|^.......
......|........
.....|^.^......
.....|.........
....|^.^.^.....
....|..........
...|^.^...^....
...|...........
..|^.^...^.^...
..|............
.|^...^.....^..
.|.............
|^.^.^.^.^...^.
|..............

Or, there's the timeline where the particle alternated going left and right at
each splitter:

.......S.......
.......|.......
......|^.......
......|........
......^|^......
.......|.......
.....^|^.^.....
......|........
....^.^|..^....
.......|.......
...^.^.|.^.^...
.......|.......
..^...^|....^..
.......|.......
.^.^.^|^.^...^.
......|........

Or, there's the timeline where the particle ends up at the same point as the
alternating timeline, but takes a totally different path to get there:

.......S.......
.......|.......
......|^.......
......|........
.....|^.^......
.....|.........
....|^.^.^.....
....|..........
....^|^...^....
.....|.........
...^.^|..^.^...
......|........
..^..|^.....^..
.....|.........
.^.^.^|^.^...^.
......|........

In this example, in total, the particle ends up on 40 different timelines.

Apply the many-worlds interpretation of quantum tachyon splitting to your
manifold diagram. In total, how many different timelines would a single tachyon
particle end up on?

*)

open Angstrom
open Solution
open CustomErrors

module Day07 : Solution = struct
  (* each beam and splitter tracks how many paths are possible up to this point *)
  type tile_t = Blank | Splitter of Z.t | Beam of Z.t
  type t = tile_t list list

  (*--------------------------------------------------------------------------*)
  (* Parser                                                                   *)
  (*--------------------------------------------------------------------------*)

  let parse_grid =
    many1
      (many1
         (choice
            [
              (char '.' >>| fun _ -> Blank);
              (char 'S' >>| fun _ -> Beam Z.one);
              (char '^' >>| fun _ -> Splitter Z.zero);
              (char '|' >>| fun _ -> Beam Z.zero);
            ])
      <* end_of_line)

  let parse_input str =
    match parse_string ~consume:Prefix parse_grid str with
    | Ok v -> Ok v
    | Error msg ->
        Printf.eprintf "%s" msg;
        Error CustomErrors.parsing_error

  (*--------------------------------------------------------------------------*)
  (* Helper Functions                                                         *)
  (*--------------------------------------------------------------------------*)

  let complete_beam (grid : tile_t list list) =
    (* complete the beam row by row, by inspecting the previous row *)
    List.fold_left
      (fun new_grid row ->
        row
        |> List.mapi (fun i tile ->
            match tile with
            | Beam _ ->
                (* The start point and all existing beams stay unchanged.*)
                tile
            | Splitter _ -> (
                (* A splitter is activated and doubles the number of paths if
                   the tile above is a beam. Otherwise it is inactive. *)
                match List.nth (List.hd new_grid) i with
                | Beam num_paths -> Splitter num_paths
                | _ -> Splitter Z.zero)
            | Blank -> (
                (* A blank is either filled with a beam if there is a beam
                   directly in the tile above, or if the tile to the top right
                   or top left contains an active splitter. *)
                match
                  ( (if i = 0 then None
                     else List.nth_opt (List.hd new_grid) (i - 1)),
                    List.nth_opt (List.hd new_grid) i,
                    List.nth_opt (List.hd new_grid) (i + 1) )
                with
                | ( (Some Blank | Some (Beam _) | None),
                    (Some Blank | Some (Splitter _)),
                    (Some Blank | Some (Beam _) | None) ) ->
                    Blank
                | ( (Some Blank | Some (Beam _) | None),
                    Some (Beam num_paths),
                    (Some Blank | Some (Beam _) | None) )
                | ( Some (Splitter num_paths),
                    (Some Blank | Some (Splitter _)),
                    (Some Blank | Some (Beam _) | None) )
                | ( (Some Blank | Some (Beam _) | None),
                    (Some Blank | Some (Splitter _)),
                    Some (Splitter num_paths) ) ->
                    Beam num_paths
                | ( Some (Splitter num_paths_left),
                    Some (Beam num_paths_middle),
                    Some (Splitter num_paths_right) ) ->
                    Beam
                      (Z.add num_paths_left
                         (Z.add num_paths_middle num_paths_right))
                | ( Some (Splitter num_paths_left),
                    (Some Blank | Some (Splitter _)),
                    Some (Splitter num_paths_right) ) ->
                    Beam (Z.add num_paths_left num_paths_right)
                | ( (Some Blank | Some (Beam _) | None),
                    Some (Beam num_paths_middle),
                    Some (Splitter num_paths_right) ) ->
                    Beam (Z.add num_paths_middle num_paths_right)
                | ( Some (Splitter num_paths_left),
                    Some (Beam num_paths_middle),
                    (Some Blank | Some (Beam _) | None) ) ->
                    Beam (Z.add num_paths_left num_paths_middle)
                | _, None, _ -> assert false))
        |> fun new_row -> new_row :: new_grid)
      [ List.hd grid ]
      (List.tl grid)
    |> List.rev

  (*--------------------------------------------------------------------------*)
  (* Solution for the first task                                              *)
  (*--------------------------------------------------------------------------*)

  let compute_simple grid =
    let active_splitters =
      complete_beam grid
      |> List.concat_map (fun row ->
          List.filter
            (function
              | Splitter num_paths when Z.leq Z.one num_paths -> true
              | _ -> false)
            row)
    in

    Printf.printf "Task 1: %i\n" (List.length active_splitters);
    Ok ()

  (*--------------------------------------------------------------------------*)
  (* Solution for the second task                                             *)
  (*--------------------------------------------------------------------------*)

  let compute_advanced grid =
    let grid_after_splits = complete_beam grid in
    let num_paths =
      List.nth grid_after_splits (List.length grid_after_splits - 1)
      |> List.filter_map (function
        | Beam num_paths -> Some num_paths
        | _ -> None)
      |> List.fold_left Z.add Z.zero
    in

    Printf.printf "Task 2: %s\n" (Z.to_string num_paths);
    Ok ()
end
